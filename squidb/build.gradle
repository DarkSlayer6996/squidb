/*
 * Copyright 2015, Yahoo Inc.
 * Copyrights licensed under the Apache 2.0 License.
 * See the accompanying LICENSE file for terms.
 */

apply plugin: 'java-library'
apply plugin: 'maven'

sourceCompatibility = JavaVersion.VERSION_1_7
targetCompatibility = JavaVersion.VERSION_1_7

sourceSets {
    main {
        java {
            srcDir 'src'
        }
    }
}

dependencies {
    api 'com.google.code.findbugs:jsr305:3.0.1' // For nullability annotations
    api 'com.google.j2objc:j2objc-annotations:1.3'
}

def siteUrl = 'https://github.com/yahoo/squidb'
def gitUrl = 'https://github.com/yahoo/squidb.git'

install {
    repositories.mavenInstaller {
        pom {
            project {
                packaging 'jar'
                name 'SquiDB'
                url siteUrl
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl
                }
            }
        }
    }
}

task sourcesJar(type: Jar) {
    from sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}
